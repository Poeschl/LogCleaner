import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.github.kt3k.coveralls' version '2.7.1'
    id 'net.researchgate.release' version '2.4.0'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "http://maven.sk89q.com/repo/" }
}

apply plugin: 'java'
apply plugin: "kotlin"
apply plugin: 'jacoco'

group = 'io.github.poeschl.bukkit'

processResources {
    filter ReplaceTokens, tokens: [
            "name"   : project.name,
            "version": project.version
    ]
}

jar {
    manifest {
        attributes("Project": project.name,
                "Version": project.version)
    }
}

shadowJar {
    configurations = [project.configurations.compile]
    classifier = null
}

coveralls {
    sourceDirs += ['src/main/kotlin']
}

jacoco {
    toolVersion = "0.7.7.201606060606"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true

        afterEvaluate {
            classDirectories = files(classDirectories.files.collect {
                fileTree(dir: it, exclude: ["**/InstanceFactory.*"])
            })
        }
    }
}

release {
    preTagCommitMessage = '[Gradle Release Plugin] - Version: '
}

dependencies {
    compileOnly "org.bukkit:bukkit:$bukkit_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testCompile "org.bukkit:bukkit:$bukkit_version"
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.2.22'
    testCompile 'org.assertj:assertj-core:3.5.2'
    testCompile 'com.nhaarman:mockito-kotlin:0.12.2'


}
