import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3"
    }
}

plugins {
    id 'com.github.kt3k.coveralls' version '2.7.1'
    id 'net.researchgate.release' version '2.4.0'
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "http://maven.sk89q.com/repo/" }
}

apply plugin: 'java'
apply plugin: 'jacoco'

group = 'de.poeschl.bukkit'

processResources {
    filter ReplaceTokens, tokens: [
            "name"   : project.name,
            "version": project.version
    ]
}

jar {
    manifest {
        attributes("Project": project.name,
                "Version": project.version)
    }
}

jacoco {
    toolVersion = "0.7.7.201606060606"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true

        afterEvaluate {
            classDirectories = files(classDirectories.files.collect {
                fileTree(dir: it, exclude: ["**/InstanceFactory.*"])
            })
        }
    }
}

dependencies {
    compile 'org.bukkit:bukkit:1.7.9-R0.2'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'org.hamcrest:hamcrest-all:1.3'


}
